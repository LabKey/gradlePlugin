apply plugin: "groovy"

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-io:1.3.2'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.1.1"
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

project.version = "0.9.1"

publishing {
    publications {
        mavenJava (MavenPublication) {
            groupId 'org.labkey.build'
            artifactId 'gradlePlugins'
            from components.java
        }
    }
}

if (project.hasProperty('artifactory_contextUrl') && project.hasProperty('artifactory_user') && project.hasProperty('artifactory_password'))
{
    artifactory {
        contextUrl = "${artifactory_contextUrl}"
        publish {
            repository {
                repoKey = project.version.endsWith('-SNAPSHOT') ? 'plugins-snapshot-local' : "plugins-release-local"
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications ('mavenJava')
                publishPom = true
                publishIvy = false
            }
        }
        resolve {
            repository {
                repoKey = project.version.endsWith('-SNAPSHOT') ? 'plugins-snapshot-local' : "plugins-release-local"
            }
        }
    }
}
